val CANNON_X_START = 16
val CANNON_Y = 216
val CANNON_DX = 4

task Cannon () {
    var x = CANNON_X_START
    var l = 0
    var r = 0
    
    par {
        every :Pico.Key.Dn {
            match it.key {
                :Key-Left  => set l = CANNON_DX
                :Key-Right => set r = CANNON_DX
            }
        }
    } with {
        every :Pico.Key.Dn | it.key == :Key-Q {
            spawn Shot([x + 8, CANNON_Y - 8], -8) in cannon_shots
        }
    } with {
        every :Pico.Key.Up {
            match it.key {
                :Key-Left  => set l = 0
                :Key-Right => set r = 0
            }
        }
    } with {
        every :Pico.Frame {
            set x = x + (r - l)
        }
    } with {
        every :Pico.Draw {
            pico.set.image.crop([[0, 0], [16, 8]])
            pico.output.draw.image([x, CANNON_Y], IMG_CANNON)
        }
    }
}